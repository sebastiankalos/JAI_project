// =====================================================================
// FCC_ee: Transfer Line for Electrons
// ARC 2 beamline definition
// =====================================================================
//
// This code is originally written by A. De Santic, C. Miraldi, now modified by Sebastian Kalos
// It now includes more annotation with (hopefully) better explanations of what various statements mean
//
// CONTACT: sebastian.kalos@physics.ox.ac.uk
// ---------------------------------------------------------------------
// GENERAL NOTES:
// A2 in an element name generally refers to an element in ARC 2
//
// in this file (as opposed to eL2L2.madx, containing arc 1, ":=" is used in drift space definitions as opposed to "=")
// this is a feature of MADX, referring to dependencies. better explanation here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch2.S6 
//
// if an element is to be repeated, it is copied and labelled by a different name, so that each element has a unique identifier
// ---------------------------------------------------------------------

// ---------------------------------------------------------------------
// here come the BENDING MAGNETS

// definition of a BENDING magnet B1:
//syntax for a bending magnet here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S3 
// ANGLE is for bending angle (logically), E1 the rotation angle for the entrance pole face, E2 the rotation angle for the exit pole face
ANGLEBc = 10*pi/180; //bend angle is 10 degrees
BEL2L2_A2_B1: RBEND, ANGLE:= ANGLEBc, E1:= 0.*ANGLEBc, E2:= 0.*ANGLEBc, L = 1.353;
BEL2L2_A2_B01: BEL2L2_A2_B1; // make an identical copy of a bending magnet, with a different name
BEL2L2_A2_B03: BEL2L2_A2_B1; // again

// definition of a BENDING magnet B2:
ANGLEBcs = 10*pi/180;    // 0.436332313
BEL2L2_A2_B2 : RBEND, ANGLE:= ANGLEBcs, E1:= 0.*ANGLEBcs, E2:= 0.*ANGLEBcs, L = 0.801;
BEL2L2_A2_B02: BEL2L2_A2_B2;

// ---------------------------------------------------------------------
// here come the DRIFT spaces:

DEL2L2_A2_D01:  DRIFT, L:=0.280; // drift space 1
DEL2L2_A2_D14:  DEL2L2_A2_D01; // drift space 14 (copy of drift 1)

DEL2L2_A2_D02:  DRIFT, L:=0.35696; // drift space 2
DEL2L2_A2_D13:  DEL2L2_A2_D02; // drift space 13 (copy of drift 2)

DEL2L2_A2_D03:  DRIFT, L:=0.957875; // drift space 3
DEL2L2_A2_D12: DEL2L2_A2_D03; // drift space 12 (copy of drift 3)

DEL2L2_A2_D04:  DRIFT, L:=0.05; // drift space 4
DEL2L2_A2_D11: DEL2L2_A2_D04; // drift space 11 (copy of drift 4)

DEL2L2_A2_D05:  DRIFT, L:= 1.05051; // drift space 5
DEL2L2_A2_D10: DEL2L2_A2_D05; // drift space 10 (copy of drift 5)

DEL2L2_A2_D06:  DRIFT, L:=    0.05  ; // drift space 6
DEL2L2_A2_D09: DEL2L2_A2_D06; // drift space 9 (copy of drift 6)

DEL2L2_A2_D07:  DRIFT, L:= 1.7250328; // drift space 7
DEL2L2_A2_D08: DEL2L2_A2_D07; //drift space 8 (copy of drift 7)

// note the symmetry of the drift copies!

// ---------------------------------------------------------------------
// here come the quadrupoles: K1 is the normal (not skew) quadrupole coefficient in the multipole expansion
// aka K1 is the quadrupole gradient normalized by B\rho. L is the length in meters
// syntax of quadrupoles here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S5 

// defocusing quadrupole 1:
QEL2L2_A2_QD1: QUADRUPOLE, K1 = -10.09346885 , L = 0.10;
QEL2L2_A2_QD01: QEL2L2_A2_QD1; //make identical copy with another name
QEL2L2_A2_QD04: QEL2L2_A2_QD1;

// defocusing quadrupole 2:
QEL2L2_A2_QD2: QUADRUPOLE, K1 = -2.034805406 , L = 0.20;
QEL2L2_A2_QD02: QEL2L2_A2_QD2; //make identical copy with another name
QEL2L2_A2_QD03: QEL2L2_A2_QD2;

// focusing quadrupole 1:
QEL2L2_A2_QF1: QUADRUPOLE, K1 =  7.585743579 , L = 0.20;
QEL2L2_A2_QF01: QEL2L2_A2_QF1; //make identical copy with another name
QEL2L2_A2_QF04: QEL2L2_A2_QF1;

// focusing quadrupole 2: 
QEL2L2_A2_QF2: QUADRUPOLE, K1 =  5.258636312 , L = 0.20;
QEL2L2_A2_QF02: QEL2L2_A2_QF2; //make identical copy with another name
QEL2L2_A2_QF03: QEL2L2_A2_QF2;

// --------------------------------------------------------------------- 
// here come sextupoles: K2 is the second coefficient in the multipole expansion.
// syntax here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S6 

// focusing sextupole: 
SEL2L2_A2_SF1 : SEXTUPOLE, K2 = 53.0873414 , L = 0.15;
SEL2L2_A2_SF01: SEL2L2_A2_SF1; //make identical copy with another name
SEL2L2_A2_SF02: SEL2L2_A2_SF1;

SEL2L2_A2_SD1 : SEXTUPOLE, K2 = -86.64583321 , L = 0.15; 
SEL2L2_A2_SD01: SEL2L2_A2_SD1; //make identical copy with another name
SEL2L2_A2_SD02: SEL2L2_A2_SD1;


// --------------------------------------------------------------------- 
// here comes the actual definition a "cell" within ARC 2, using the elements defined above
// syntax of a beamline here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch13
 EL2L2_A2_TBACELL: LINE=(
		 QEL2L2_A2_QD01, // defocusing quad 1
 		 DEL2L2_A2_D01,  // drift 1
 		 QEL2L2_A2_QF01, // focusing quad 1
 		 DEL2L2_A2_D02,  // drift 2
 		 BEL2L2_A2_B01,  // bend 1
 		 DEL2L2_A2_D03,  // drift 3
 		 QEL2L2_A2_QF02, // focusing quad 2
 		 DEL2L2_A2_D04,  // drift 4
 		 SEL2L2_A2_SF01, // focusing sextupole 1
 		 DEL2L2_A2_D05,  // drift 5
 		 QEL2L2_A2_QD02, // defocusing quad 2
		 DEL2L2_A2_D06,  // drift 6
  		 SEL2L2_A2_SD01, // defocusing sextupole 1
 		 DEL2L2_A2_D07,  // drift 7
 		 BEL2L2_A2_B02,  // bend 2
 		 DEL2L2_A2_D08,  // drift 8
  		 SEL2L2_A2_SD02, // defocusing sextupole 2
  		 DEL2L2_A2_D09,  // drift 9
 		 QEL2L2_A2_QD03, // defocusing quadrupole 3
 		 DEL2L2_A2_D10,  // drift 10
 		 SEL2L2_A2_SF02, // focusing sextupole 2
 		 DEL2L2_A2_D11,  // drift 11
 		 QEL2L2_A2_QF03, // focusing quadrupole 3
 		 DEL2L2_A2_D12,  // drift 12
 		 BEL2L2_A2_B03,  // bend 3
 		 DEL2L2_A2_D13,  // drift 13
 		 QEL2L2_A2_QF04, // focusing quadrupole 4
 		 DEL2L2_A2_D14,  // drift 14
 		 QEL2L2_A2_QD04  // defocusing quad 4
		 );
 
// the ARC 1 beamline is actually three of the "cells" defined above
// note that Arc 1 is composed of 6 cells, probably bending by 180 degrees
EL2L2_A2: LINE=(3*EL2L2_A2_TBACELL); // 90 degree arc

return;
