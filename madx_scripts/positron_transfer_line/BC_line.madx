// =====================================================================
// FCC_ee: Bunch Compressor for the end of a positron transfer line (from damping ring back to common linac)
// =====================================================================
//
// Description:
// this file calls beamline definitions of the compressor sequence, computes twiss functions, and performs phase space tracking
// ---------------------------------------------------------------------

TITLE, S="FCC_ee: Bunch Compressor"; // just a name of this thing, obviously

// ---------------------------------------------------------------------
// OPTIONS
option,echo = false,warn = true; // switch off copying the input to the standard output but keep issuing warning statements
// a better explanation of echo, warn statements can be found here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch4.S5

// ---------------------------------------------------------------------
// load files

print, text='LOADING BUNCH COMPRESSOR';
CALL, FILE='PTL_BC.madx'; //calling a file with Bunch Compressor Definition (as written by Sebastian Kalos)
CALL, FILE='PTL_DBA.madx'; //calling a file with Bunch Compressor Definition (as written by Sebastian Kalos)
CALL, FILE='BC_tune.madx'; //calling a file with Bunch Compressor Definition (as written by Sebastian Kalos)


print, text='LOADING FODO CELL';
CALL, FILE='PTL_fodo.madx';
CALL, FILE='PTL_a1.madx';

// ---------------------------------------------------------------------
// BEAM definition
	//Notes for the BEAM definition below:
		// parameters found here: https://iopscience.iop.org/article/10.1088/1742-6596/1067/6/062023/meta
		// deltap = momentum deviation (relative, i.e. deltap = dp/p). Taken as 0.1% 
		// If radiate=true, synchrotron radiation is considered in all dipoles
		// energy is in GeV by default, charge in elementary charge e: https://mad.web.cern.ch/mad/webguide/manual.html#Ch1.S8
		// total charge cca 4.8nC, which means cca 3.0e10 positrons in a bunch

BEAM,particle=positron,energy=1.54,radiate= false, deltap = 0.001; 

// ---------------------------------------------------------------------
// BEAMLINE definition (sequence named BC_line)

// syntax of a general beamline here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch13
BC_line: LINE=(
			 //PTL_BC_ALL,	 // only BUNCH COMPRESSOR (by Sebastian Kalos, see PTL_BC.madx)
			 //PTL_FD, // FODO cell
			 //PTL_DBA_ALL, // Double Bend Achromat Bunch Compressor
			 PTL_BC_TUNE_ALL
			 );

// ---------------------------------------------------------------------
// Survey of Twiss parameters
print, text='MAKING SURVEY';

//load the sequence BC_line defined above (without the USE command, nothing will run)
//better explanation: https://mad.web.cern.ch/mad/webguide/manual.html#Ch4.S10
use, sequence=BC_line;

// while saving the twiss parameters later, only give columns containing the following information (disregard other):
select, flag=twiss,sequence=BC_line,column=name,keyword,s,l,betx,alfx,dx,bety,alfy,dy,angle,K1L,ddx;

// now, calculate the lattice functions using the TWISS command, using the PTL sequence (USE call above is enough)
// documentation here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch20
// not sure what RMATRIX stands for: "If this flag is used the the one-turn map at the location of every element is calculated and prepared for storage in the twiss table".
// better to see the documentation, I'm keeping it in there.
// dx is initial value for horizonatl dispersion, dy vertical dispersion, dpx and dpy are relative initial momentum spread
// alfx,alfy,betx,bety are initial values of alpha and beta functions ; beta has initial values read from file PTL_beta0.madx
twiss,RMATRIX,CHROM=TRUE,dx = 1e-3,dy=1e-3, dpy=0, dpx = 0,alfx = 0.,alfy = 0.,betx=8,bety=8,
	     	  file="BC.out",save; // save twiss parameters in file "PTL.out"

// ---------------------------------------------------------------------
// Here starts a PARTICLE TRACKING module (I wanna see the phase space!)

MAKETHIN, SEQUENCE=BC_line, STYLE=TEAPOT; // for tracking (at least the basic one, not PTC), one needs to convert te sequence to thin elements

TRACK, ONEPASS=true,file="tracking_data/BC_track.out",dump,onetable; //request the track particles in phase space module
// onetable flag makes sure all particles are written in a single file, instead of one file per particle
// onepass = a single pass through the transfer line (not sure if needed but idk if it's otherwise assumed to be periodic)

OBSERVE, PLACE=CAV_BC_1; // setting observation point (element name) for tracking (apart from start and end, those are recorded by default)

n=0;
while (n < 5000) { //generate a ton of particles to be tracked in a while loop (START command)
//initialize particle to be tracked:
start, x= 1e-4*GAUSS(), px=1e-5*GAUSS(), y= 1e-4*GAUSS(), py=1e-5*GAUSS(),
	   T=1e-3*GAUSS(), //5mm bunch length initially (as per https://iopscience.iop.org/article/10.1088/1742-6596/1067/6/062023/meta)
	   PT=0.1e-2*GAUSS(); // 0.1% energy spread after DR (aslo as per https://iopscience.iop.org/article/10.1088/1742-6596/1067/6/062023/meta)
  n=n+1;
};
RUN; //actual command that starts the tracking process
endtrack; //end tracking

return; 
