
// ---------------------------------------------------------------------
// here come the BENDING MAGNETS

// definition of a BENDING magnet B1:
//syntax for a bending magnet here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S3 
// ANGLE is for bending angle (logically), E1 the rotation angle for the entrance pole face, E2 the rotation angle for the exit pole face
ANGLE_LEFT = 10*pi/180; //bend angle is 10 degrees
BPTL_LEFT_B1: RBEND, ANGLE:= ANGLE_LEFT, E1:= 0.*ANGLE_LEFT, E2:= 0.*ANGLE_LEFT, L = 1.353;
BPTL_LEFT_B01: BPTL_LEFT_B1; // make an identical copy of a bending magnet, with a different name
BPTL_LEFT_B03: BPTL_LEFT_B1; // again

// definition of a BENDING magnet B2:
ANGLE_LEFT = 10*pi/180;    // 0.436332313
BPTL_LEFT_B2 : RBEND, ANGLE:= ANGLE_LEFT, E1:= 0.*ANGLE_LEFT, E2:= 0.*ANGLE_LEFT, L = 0.801;
BPTL_LEFT_B02: BPTL_LEFT_B2;

// ---------------------------------------------------------------------
// here come the DRIFT spaces:

DPTL_LEFT_D01:  DRIFT, L:=0.280; // drift space 1
DPTL_LEFT_D14:  DPTL_LEFT_D01; // drift space 14 (copy of drift 1)

DPTL_LEFT_D02:  DRIFT, L:=0.35696; // drift space 2
DPTL_LEFT_D13:  DPTL_LEFT_D02; // drift space 13 (copy of drift 2)

DPTL_LEFT_D03:  DRIFT, L:=0.957875; // drift space 3
DPTL_LEFT_D12: DPTL_LEFT_D03; // drift space 12 (copy of drift 3)

DPTL_LEFT_D04:  DRIFT, L:=0.05; // drift space 4
DPTL_LEFT_D11: DPTL_LEFT_D04; // drift space 11 (copy of drift 4)

DPTL_LEFT_D05:  DRIFT, L:= 1.05051; // drift space 5
DPTL_LEFT_D10: DPTL_LEFT_D05; // drift space 10 (copy of drift 5)

DPTL_LEFT_D06:  DRIFT, L:=    0.05  ; // drift space 6
DPTL_LEFT_D09: DPTL_LEFT_D06; // drift space 9 (copy of drift 6)

DPTL_LEFT_D07:  DRIFT, L:= 1.7250328; // drift space 7
DPTL_LEFT_D08: DPTL_LEFT_D07; //drift space 8 (copy of drift 7)

// note the symmetry of the drift copies!

// ---------------------------------------------------------------------
// here come the quadrupoles: K1 is the normal (not skew) quadrupole coefficient in the multipole expansion
// aka K1 is the quadrupole gradient normalized by B\rho. L is the length in meters
// syntax of quadrupoles here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S5 

// defocusing quadrupole 1:
QPTL_LEFT_QD1: QUADRUPOLE, K1 = -10.09346885 , L = 0.10;
QPTL_LEFT_QD01: QPTL_LEFT_QD1; //make identical copy with another name
QPTL_LEFT_QD04: QPTL_LEFT_QD1;

// defocusing quadrupole 2:
QPTL_LEFT_QD2: QUADRUPOLE, K1 = -2.034805406 , L = 0.20;
QPTL_LEFT_QD02: QPTL_LEFT_QD2; //make identical copy with another name
QPTL_LEFT_QD03: QPTL_LEFT_QD2;

// focusing quadrupole 1:
QPTL_LEFT_QF1: QUADRUPOLE, K1 =  7.585743579 , L = 0.20; 
QPTL_LEFT_QF01: QPTL_LEFT_QF1; //make identical copy with another name
QPTL_LEFT_QF04: QPTL_LEFT_QF1;

// focusing quadrupole 2: 
QPTL_LEFT_QF2: QUADRUPOLE, K1 =  5.258636312 , L = 0.20;
QPTL_LEFT_QF02: QPTL_LEFT_QF2; //make identical copy with another name
QPTL_LEFT_QF03: QPTL_LEFT_QF2;

// --------------------------------------------------------------------- 
// here come sextupoles: K2 is the second coefficient in the multipole expansion.
// syntax here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch11.S6 

// focusing sextupole: 
SPTL_LEFT_SF1 : SEXTUPOLE, K2 = 53.0873414 , L = 0.15;
SPTL_LEFT_SF01: SPTL_LEFT_SF1; //make identical copy with another name
SPTL_LEFT_SF02: SPTL_LEFT_SF1;

SPTL_LEFT_SD1 : SEXTUPOLE, K2 = -86.64583321 , L = 0.15;
SPTL_LEFT_SD01: SPTL_LEFT_SD1; //make identical copy with another name
SPTL_LEFT_SD02: SPTL_LEFT_SD1;


// --------------------------------------------------------------------- 
// here comes the actual definition a "cell" within ARC 2, using the elements defined above
// syntax of a beamline here: https://mad.web.cern.ch/mad/webguide/manual.html#Ch13
 PTL_BC_BEND_LEFT: LINE=(
		 QPTL_LEFT_QD01, // defocusing quad 1
 		 DPTL_LEFT_D01,  // drift 1
 		 QPTL_LEFT_QF01, // focusing quad 1
 		 DPTL_LEFT_D02,  // drift 2
 		 BPTL_LEFT_B01,  // bend 1
 		 DPTL_LEFT_D03,  // drift 3
 		 QPTL_LEFT_QF02, // focusing quad 2
 		 DPTL_LEFT_D04,  // drift 4
 		 SPTL_LEFT_SF01, // focusing sextupole 1
 		 DPTL_LEFT_D05,  // drift 5
 		 QPTL_LEFT_QD02, // defocusing quad 2
		 DPTL_LEFT_D06,  // drift 6
  		 SPTL_LEFT_SD01, // defocusing sextupole 1
 		 DPTL_LEFT_D07,  // drift 7
 		 BPTL_LEFT_B02,  // bend 2
 		 DPTL_LEFT_D08,  // drift 8
  		 SPTL_LEFT_SD02, // defocusing sextupole 2
  		 DPTL_LEFT_D09,  // drift 9
 		 QPTL_LEFT_QD03, // defocusing quadrupole 3
 		 DPTL_LEFT_D10,  // drift 10
 		 SPTL_LEFT_SF02, // focusing sextupole 2
 		 DPTL_LEFT_D11,  // drift 11
 		 QPTL_LEFT_QF03, // focusing quadrupole 3
 		 DPTL_LEFT_D12,  // drift 12
 		 BPTL_LEFT_B03,  // bend 3
 		 DPTL_LEFT_D13,  // drift 13
 		 QPTL_LEFT_QF04, // focusing quadrupole 4
 		 DPTL_LEFT_D14,  // drift 14
 		 QPTL_LEFT_QD04  // defocusing quad 4
		 );

return;
